plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
    // ideally matches the dependency version of the latest NMS module
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    compileOnly('LibsDisguises:LibsDisguises:9.5.2') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.comphenix.protocol'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.6') {
        exclude group: 'org.bukkit'
    }
    compileOnly 'me.clip:placeholderapi:2.10.3'
    compileOnly 'org.black_ixx:playerpoints:3.1.0'
    compileOnly 'com.google.code.gson:gson:2.2.4'
    // WG 7.0.3 was last version to support Java <16
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.3'
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    // same version used in 1.17.1
    compileOnly 'com.mojang:authlib:2.3.31'
    implementation ('com.zaxxer:HikariCP:4.0.3') {
        exclude group: 'org.slf4j'
    }
    // 'api' so it's passed down to NMS modules
    api 'com.github.cryptomorin:XSeries:8.7.1'
}
shadowJar {
    // XParticle is big and not required so don't shade it
    exclude 'com/cryptomorin/xseries/particles/*'
    relocate ('com.zaxxer.hikari', 'be.isach.ultracosmetics.shaded.hikari')
    relocate ('com.cryptomorin.xseries', 'be.isach.ultracosmetics.shaded.xseries')
}
